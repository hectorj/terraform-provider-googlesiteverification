---
page_title: "googlesiteverification_dns Resource - terraform-provider-googlesiteverification"
subcategory: ""
description: |-
  https://developers.google.com/site-verification
---

# googlesiteverification_dns (Resource)
A resource that verifies ownership of a website or domain.
https://developers.google.com/site-verification


## Example Usage
```hcl
terraform {
  required_providers {
    googlesiteverification = {
      source  = "hectorj/googlesiteverification"
    }
  }
}

resource "google_project_service" "siteverification" {
  service = "siteverification.googleapis.com"
}

data "googlesiteverification_dns_token" "domain" {
  domain     = var.domain_name
  depends_on = [google_project_service.siteverification]
}

resource "google_dns_managed_zone" "domain" {
  name     = replace(var.domain_name, ".", "-")
  dns_name = "${var.domain_name}."
}

resource "google_dns_record_set" "domain_ns_records" {
  name         = "${var.domain_name}."
  type         = "NS"
  ttl          = 60
  managed_zone = google_dns_managed_zone.domain.name
  rrdatas      = google_dns_managed_zone.domain.name_servers
}

resource "google_dns_record_set" "domain" {
  managed_zone = google_dns_managed_zone.domain.name
  name         = "${data.googlesiteverification_dns_token.domain.record_name}."
  rrdatas      = [data.googlesiteverification_dns_token.domain.record_value]
  type         = data.googlesiteverification_dns_token.domain.record_type
  ttl          = 60
}

resource "googlesiteverification_dns" "domain" {
  domain     = var.domain_name
  token      = data.googlesiteverification_dns_token.domain.record_value
  depends_on = [google_dns_record_set.domain_ns_records]
  timeouts {
    create = "30s"
  }
}
```
<!-- schema generated by tfplugindocs -->
## Argument Reference
The following arguments are supported:
### Required

- `domain` (String) The domain you want to verify.
- `token` (String) The token you got from data.googlesiteverification_dns_token. This forces a new verification in case the token changes.

### Optional

- `timeouts` (Block, Optional) (see [below for nested schema](#nestedblock--timeouts))

### Read-Only

- `id` (String) The ID of this resource.


### Timeouts
This resource provides the following [Timeouts](https://www.terraform.io/docs/configuration/resources.html#timeouts) configuration options:

Optional:

- `create` (String) duration including unit "30s", "2m"


## Import
```bash
$ terraform import "googlesiteverification_dns.example" "yourdomain.example.com"
```
